ext.isArm = true
ext.buildPlatform = 'arm'

model {
    platforms {
	arm {
	    architecture 'arm'
	    operatingSystem 'linux'
	}
    }
    toolChains {
        gcc(Gcc) {
            target("arm") {
                // We use a custom-built cross compiler with the prefix arm-frc-linux-gnueabi-<util name>
                // If this ever changes, the prefix will need to be changed here
                cppCompiler.executable = compilerPrefixToUse + cppCompiler.executable
                linker.executable = compilerPrefixToUse + linker.executable
                assembler.executable = compilerPrefixToUse + assembler.executable
                // Gradle auto-adds the -m32 argument to the linker and compiler. Our compiler only supports
                // arm, and doesn't understand this flag, so it is removed from both
                cppCompiler.withArguments { args ->
                    args << '-std=c++1y' << '-Wformat=2' << '-Wall' << '-Wextra' << '-Werror' << '-pedantic'
                    args << '-Wno-psabi' << '-Wno-unused-parameter' << '-fPIC' << '-rdynamic'
                    //TODO: When the compiler allows us to actually call deprecated functions from within
                    // deprecated function, remove this line (this will cause calling deprecated functions
                    // to be treated as a warning rather than an error).
                    args << '-Wno-error=deprecated-declarations' << '-pthread'
                    args.remove('-m32')
                }
                linker.withArguments { args ->
                    args << '-rdynamic' << '-pthread'
                    args.remove('-m32')
                }
                staticLibArchiver.executable = compilerPrefixToUse + staticLibArchiver.executable
            }
        }
        // Workaround for OS X. Macs for some reason want to use Xcode's gcc
        // (which just wraps Clang), so we have to explicitly make it so
        // that trying to compile with Clang will call gcc instead
        macGcc(Clang) {
            target('arm') {
                // We use a custom-built cross compiler with the prefix arm-frc-linux-gnueabi-<util name>
                // If this ever changes, the prefix will need to be changed here
                cppCompiler.executable = compilerPrefixToUse + 'g++'
                linker.executable = compilerPrefixToUse + 'g++'
                assembler.executable = compilerPrefixToUse + 'gcc'
                // Gradle auto-adds the -m32 argument to the linker and compiler. Our compiler only supports
                // arm, and doesn't understand this flag, so it is removed from both
                cppCompiler.withArguments { args ->
                    args << '-std=c++1y' << '-Wformat=2' << '-Wall' << '-Wextra' << '-Werror' << '-pedantic'
                    args << '-Wno-psabi' << '-Wno-unused-parameter' << '-fPIC' << '-O0' << '-g3' << '-rdynamic'
                    //TODO: When the compiler allows us to actually call deprecated functions from within
                    // deprecated function, remove this line (this will cause calling deprecated functions
                    // to be treated as a warning rather than an error).
                    args << '-Wno-error=deprecated-declarations' << '-pthread'
                    args.remove('-m32')
                }
                linker.withArguments { args ->
                    args << '-rdynamic' << '-pthread'
                    args.remove('-m32')
                }
                staticLibArchiver.executable = compilerPrefixToUse + 'ar'
            }
        }
    }
}

ext.setupReleaseDefines = { cppCompiler, linker ->
    cppCompiler.args '-O2', '-g'
}

ext.setupDebugDefines = { cppCompiler, linker ->
    cppCompiler.args '-g', '-O0'
}

ext.debugStripSetup = {
    if (!project.hasProperty('debug')) {
        project.tasks.whenObjectAdded { task ->
            def name = task.name.toLowerCase()
            if (name.contains('link') && name.contains('sharedlibrary')) {
                def library = task.outputFile.absolutePath
                def debugLibrary = task.outputFile.absolutePath + ".debug"
                task.doLast {
                    exec { 
                      ignoreExitValue = true 
                      commandLine "${compilerPrefixToUse}objcopy", '--only-keep-debug', library, debugLibrary 
                    }
                    exec { 
                      ignoreExitValue = true 
                      commandLine "${compilerPrefixToUse}strip", '-g', library 
                    }
                    exec { 
                      ignoreExitValue = true 
                      commandLine "${compilerPrefixToUse}objcopy", "--add-gnu-debuglink=$debugLibrary", library 
                    }
                }
            }
        }
    }
}
